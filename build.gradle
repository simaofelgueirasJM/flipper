buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${GRADLE_BINTRAY_PLUGIN_VERSION}"
        classpath "com.github.dcendents:android-maven-gradle-plugin:${ANDROID_MAVEN_GRADLE_PLUGIN_VERSION}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${GRADLE_VERSIONS_PLUGIN_VERSION}"
    }
}

plugins {
    id 'de.undercouch.download' version '3.4.3'
}

ext.isSnapshot = { VERSION_NAME.endsWith('-SNAPSHOT') }
ext.isRelease = { ['uploadArchives', 'bintrayUpload'].any { gradle.startParameter.taskNames.contains(it) } }

subprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        jcenter()

        if (isSnapshot()) {
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        }
    }
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 25
    compileSdkVersion = 27
    buildToolsVersion = '28.0.3'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        // Android support
        supportAnnotations : 'com.android.support:support-annotations:27.1.1',
        supportAppCompat   : 'com.android.support:appcompat-v7:27.1.1',
        supportCoreUi      : 'com.android.support:support-core-ui:27.1.1',
        supportRecyclerView: 'com.android.support:recyclerview-v7:27.1.1',
        supportConstraintLayout: 'com.android.support.constraint:constraint-layout:1.1.0',
        supportEspresso    : 'com.android.support.test.espresso:espresso-core:2.2.2',
        supportDesign      : 'com.android.support:design:27.1.1',
        supportEspressoIntents : 'com.android.support.test.espresso:espresso-intents:2.2.2',
        supportTestRunner  : 'com.android.support.test:runner:1.0.2',
        // Arch
        archPaging         : 'android.arch.paging:runtime:1.0.0',
        // First-party
        soloader           : 'com.facebook.soloader:soloader:0.5.1',
        screenshot         : 'com.facebook.testing.screenshot:core:0.5.0',
        // Annotations
        jsr305             : 'com.google.code.findbugs:jsr305:3.0.1',
        inferAnnotations   : 'com.facebook.infer.annotation:infer-annotation:0.11.2',
        // Litho
        lithoAnnotations   : "com.facebook.litho:litho-annotations:$LITHO_VERSION",
        lithoCore          : "com.facebook.litho:litho-core:$LITHO_VERSION",
        lithoWidget        : "com.facebook.litho:litho-widget:$LITHO_VERSION",
        lithoProcessor     : "com.facebook.litho:litho-processor:$LITHO_VERSION",
        lithoFresco        : "com.facebook.litho:litho-fresco:$LITHO_VERSION",
        lithoTesting       : "com.facebook.litho:litho-testing:$LITHO_VERSION",
        // Debugging and testing
        guava              : 'com.google.guava:guava:20.0',
        robolectric        : 'org.robolectric:robolectric:3.3',
        junit              : 'junit:junit:4.12',
        hamcrest           : 'org.hamcrest:hamcrest-library:1.3',
        mockito            : 'org.mockito:mockito-core:1.9.5',
        stetho             : 'com.facebook.stetho:stetho:1.5.0',
        okhttp3            : 'com.squareup.okhttp3:okhttp:3.10.0',
        leakcanary         : 'com.squareup.leakcanary:leakcanary-android:1.6.1',
        // Plugin dependencies
        rhino              : 'org.mozilla:rhino:1.7.10',
]
